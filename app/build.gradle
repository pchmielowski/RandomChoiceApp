plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'com.google.devtools.ksp' version "1.7.20-1.0.6"
    id 'com.squareup.sqldelight'
}

check.dependsOn assembleAndroidTest

android {
    signingConfigs {
        release {
            // To decode the file, run `git secret reveal` command.
            def propsFile = file('keystore.properties')
            if (propsFile.exists()) {
                def props = new Properties()
                props.load(new FileInputStream(propsFile))

                storeFile = file('keystore.jks')
                storePassword = props['storePassword']
                keyPassword = props['keyPassword']
                keyAlias = props['keyAlias']
            }
        }
    }
    compileSdk 32
    defaultConfig {
        applicationId "net.chmielowski.randomchoice"
        minSdk 26
        targetSdk 32
        versionCode 24
        versionName "1.$versionCode"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    namespace "net.chmielowski.randomchoice"
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
            signingConfig signingConfigs.release
        }
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion "1.3.2"
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    lintOptions {
        warningsAsErrors true
    }
    sourceSets {
        test { kotlin.srcDirs += "$projectDir/src/sharedTest" }
        androidTest { kotlin.srcDirs += "$projectDir/src/sharedTest" }
    }
}

sqldelight {
    Database {
        schemaOutputDirectory = file("src/main/sqldelight/databases")
    }
}

tasks.withType(Test) {
    failFast = true
    testLogging {
        testLogging.exceptionFormat = 'full'
    }
}

// Workaround for https://github.com/google/ksp/issues/37
kotlin {
    sourceSets {
        debug {
            kotlin.srcDir("build/generated/ksp/debug/kotlin")
        }
        release {
            kotlin.srcDir("build/generated/ksp/release/kotlin")
        }
    }
}

dependencies {
    implementation 'androidx.activity:activity-compose:1.5.1'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation 'androidx.compose.material3:material3:1.0.0-alpha15'
    implementation "androidx.compose.material:material-icons-core:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4"

    // Accompanist
    final accompanist_version = "0.23.1"
    implementation "com.google.accompanist:accompanist-systemuicontroller:$accompanist_version"
    implementation "com.google.accompanist:accompanist-insets:$accompanist_version"

    // Destinations
    final destinations_version = "1.4.2-beta"
    implementation "io.github.raamcosta.compose-destinations:animations-core:$destinations_version"
    ksp "io.github.raamcosta.compose-destinations:ksp:$destinations_version"

    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"

    // MVI
    final mvi_version = "3.0.1"
    implementation "com.arkivanov.mvikotlin:mvikotlin:$mvi_version"
    implementation "com.arkivanov.mvikotlin:mvikotlin-main:$mvi_version"
    implementation "com.arkivanov.mvikotlin:mvikotlin-extensions-coroutines:$mvi_version"

    // SqlDelight
    implementation "com.squareup.sqldelight:android-driver:$sql_delight_version"
    implementation "com.squareup.sqldelight:coroutines-extensions-jvm:$sql_delight_version"
    testImplementation "com.squareup.sqldelight:sqlite-driver:$sql_delight_version"
    androidTestImplementation "com.squareup.sqldelight:sqlite-driver:$sql_delight_version"
    // Workaround for https://github.com/cashapp/sqldelight/issues/1397
    testImplementation "org.xerial:sqlite-jdbc:3.34.0"

    // Koin
    final koin_version = "3.2.0"
    implementation "io.insert-koin:koin-androidx-compose:$koin_version"
    testImplementation "io.insert-koin:koin-test:$koin_version"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.8.1'
    testImplementation "org.assertj:assertj-core:3.22.0"
    testImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.8.1'
}
